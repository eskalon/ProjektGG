apply plugin: "java"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

import org.gradle.internal.os.OperatingSystem

project.ext.mainClassName = "de.eskalon.gg.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../game/assets");

tasks.register('run', JavaExec) {
    dependsOn classes
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true

    if (OperatingSystem.current() == OperatingSystem.MAC_OS) {
        jvmArgs += "-XstartOnFirstThread"
    }
}

tasks.register('debug', JavaExec) {
    dependsOn classes
    mainClass = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
    debug = true
}

// Generate a fat jar
jar { 
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
	
    manifest {
        attributes (
        	"Main-Class": project.mainClassName,
        	"Implementation-Version": project.version
        )
    }
}
jar.dependsOn configurations.runtimeClasspath

tasks.register('dist', Jar) {
    archiveName appName + "_" + version + ".zip"
    
    //duplicatesStrategy(DuplicatesStrategy.INCLUDE)
	
    from fileTree(project.assetsDir) {
        exclude("CONTRIBUTORS.md")
    }
    from file("../LICENSE");
    from file("../CONTRIBUTORS.md");
    from jar.outputs.files
}
dist.dependsOn jar

eclipse {
    project {
        name = appName + "-desktop"
        linkedResource name: "assets", type: "2", location: "PARENT-1-PROJECT_LOC/game/assets"
    }
}
